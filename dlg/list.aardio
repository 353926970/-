if( _version == null ){
	return;
}
import win.ui;
/*DSG{{*/
var winlist = win.form(text="列表";right=99;bottom=39;bgcolor=3618615;border="none";exmode="toolwindow";max=false;min=false;mode="popup")
winlist.add(
button_menu={cls="plus";text="QuickViewPic";left=0;top=0;right=100;bottom=20;bgcolor=-13951306;color=16777215;font=LOGFONT(h=-14);z=1};
listbox={cls="listbox";left=0;top=20;right=100;bottom=100;bgcolor=3618615;color=16777215;integralHeight=0;items={"1";"2";"3";"4";"5";"6";"7";"8";"9";"10";"11";"12";"13";"14";"15";"16";"17";"18"};z=2}
)
/*}}*/

/*移动窗口{{*/
movewin = function( v ){
	var scrw, scrh = win.getScreen();
	var x, y, z;
	select(..parameter.Dockside) {
		case 'Left' x, y, z = 0, 200, -97;
		case 'Right' x, y, z = scrw-100, 200, 97;
		else x, y, z = 0, 200, -97;
	}
	select( v ) {
		case 'Show' winlist.setPos(x,y);
		case 'Hide' winlist.setPos(x+z,y);
		else winlist.setPos(x+z,y);
	}
}
/*}}*/

/*初始化窗口、列表框及数据{{*/
var value ={};
initialize = function(){
	import fsys;
	movewin();
	select( ..parameter.Mode ) {
		case 'Default' {
			value = fsys.list( io._exedir,,{"*.jpg";"*.png";"*.bmp"});
			var num = table.count( value )/2;
			if( num == 0 )
				winlist.listbox.items = {"当前目录无图片";"请检查设置"};
			else {
				var items = {};
				for(i=1;num;1){
					items[i] = value[i];
					value[i] = null;
				}
				winlist.listbox.items = items;
			}
		}
		case 'CustomFolder' {
			if( ..parameter.Folder == "" ){
				winlist.msgbox("未指定自定义路径，程序已自动调整为默认模式。","提示");
				..parameter.Mode = 'Default';
				value = fsys.list( io._exedir,,{"*.jpg";"*.png";"*.bmp"});
			}
			else if( !io.exist(..parameter.Folder) ){
				winlist.msgbox("自定义路径不存在，程序已自动调整为默认模式。","提示");
				..parameter.Mode = 'Default';
				value = fsys.list( io._exedir,,{"*.jpg";"*.png";"*.bmp"});
			}
			else
				value = fsys.list( ..parameter.Folder,,{"*.jpg";"*.png";"*.bmp"});
			var num = table.count( value )/2;
			if( num == 0 )
				winlist.listbox.items = {"当前目录无图片";"请检查设置"};
			else {
				var items = {};
				for(i=1;num;1){
					items[i] = value[i];
					value[i] = null;
				}
				winlist.listbox.items = items;
			}
		}
		case 'CustomList' {
			value = {};
			var num = table.count( ..parameter.Items );
			if( num == 0 )
				winlist.listbox.items = {"未配置图片列表";"请检查设置"};
			else {
				var items = {};
				for(i=1;num;1){
					var j = string.lastIndexOf( ..parameter.Items[i], "\" );
					items[i] = string.right( ..parameter.Items[i] , -j-1 );
				}
				winlist.listbox.items = items;
			}
		}
		case 'Clip' {
			value = {};
			var num = table.count( ..clip );
			if( num == 0 )
				winlist.listbox.items = {"请先进入设置";"手动配置剪贴板";};
			else {
				var items = {};
				for(i=1;table.count( ..clip );1){
					items[..clip[i][1]] = ..clip[i][2];
					value[..clip[i][2]] = ..clip[i][3];
				}
				winlist.listbox.items = items;
			}
		}
	}
	winlist.listbox.height = 15*table.count(winlist.listbox.items);
}
/*}}*/

/*设置关闭按钮颜色{{*/
winlist.button_menu.skin({
	background={
		default=0xFFB61E2B;
		disabled=0xFFCCCCCC;
		hover=0xFFB61E2B
	};
	color={
		default=0xFFFFFFFF;
		disabled=0xFFFFFFFF
	}
})
/*}}*/

/*菜单按钮事件{{*/
winlist.button_menu.onMouseEnter = function(wParam,lParam){
	movewin("Show");
	winlist.button_menu.text = 'Menu';
}

winlist.button_menu.onMouseMove = function(wParam,lParam){
	movewin("Show");
}

winlist.button_menu.onMouseLeave = function(wParam,lParam){
	movewin("Hide");
	winlist.button_menu.text = 'QuickViewPic';
}

winlist.button_menu.onMouseClick = function(wParam,lParam){
    import win.ui.menu;
	var menu_pop = win.ui.popmenu(winlist);
	menu_pop.addTable( {
		{ "QuickViewPic";};
 		{ /*分隔线*/ };
		{ "设置";  function(id){
			winlist.show(false);
			var n = winlist.loadForm("\dlg\setting.aardio").doModal();
			if( ..parameter.Mode == 'Clip' )
				initialize();
			elseif( n )
				initialize();
			winlist.show();
		} };
		{ "主页"; function(id){
			import process;
			process.openUrl("https://github.com/xenon2333/QuickViewPic");
 		} };
		{ "关于"; function(id){
			var str = "QuickViewPic v"++_version++'\r\nWritten by Xenon\r\nEmail：xenon2333@qq.com';
			winlist.msgbox(str,"关于");
 		} };
		{ /*分隔线*/ };
		{ "退出"; function(id){
			winlist.close();
 		} };
	} );
	menu_pop.enable(1,false);
	menu_pop.popup();
}
/*}}*/

/*鼠标追踪{{*/
import win.ui.tracker;
var track_listbox = win.ui.tracker(winlist.listbox);
track_listbox.onMouseMove = function(wParam,lParam){
	var x,y = win.getMessagePos();
	var id = winlist.listbox.hitTest(x,y,true);
	winlist.listbox.selIndex = id;
	if( ..parameter.Mode == 'CustomList')
		id = ..parameter.Items[id];
	else
		id = value[winlist.listbox.selText];
	publish("ShowPic", id);
	movewin("Show");
}
track_listbox.onMouseLeave = function(wParam,lParam){
	winlist.listbox.selIndex = 0;
	publish("Hide",);
	movewin("Hide");
	win.setTopmost(winlist.hwnd);//窗口置顶
}
/*}}*/

winlist.listbox.orphanWindow();
initialize();
winlist.loadForm("\dlg\pic.aardio");
winlist.show();
win.setTopmost(winlist.hwnd);//窗口置顶
win.loopMessage();
return winlist;
