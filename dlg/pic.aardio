import win.ui;
/*DSG{{*/
var winpic = win.form(text="Pic";right=19;bottom=19;border="none";exmode="toolwindow";max=false;min=false;mode="popup";title=false)
winpic.add(
picturebox={cls="picturebox";left=0;top=0;right=20;bottom=20;z=1}
)
/*}}*/

if( !winpic.parent ){
	return; //阻止该窗口单独启动
}
import gdip.bitmap;

var history, bithwnd; //历史记录和位图句柄

/*调整图片框大小及窗口位置{{*/
adapt = function( w, h ){
	var scrw, scrh = win.getScreen();
	var dx, dy;
	if( h > scrh : w > scrw ){
		if( h/w > scrh/(scrw-120 ) ){
			winpic.picturebox.height = scrh;
			winpic.picturebox.width = math.floor(w*scrh/h);
			dx, dy = winpic.picturebox.width, 0;
		}
		else {
			winpic.picturebox.height = math.floor(h*(scrw-120)/w);
			winpic.picturebox.width = scrw -120;
			dx, dy = winpic.picturebox.width, scrh/2-math.floor(winpic.picturebox.height/2);
		}
	}
	else {
		winpic.picturebox.width = w;
		winpic.picturebox.height = h;
		dx, dy = winpic.picturebox.width, scrh/2-math.floor(h/2);
	}
	select(..parameter.Dockside) {
		case 'Left' winpic.setPos( 120, dy );
		case 'Right' winpic.setPos( scrw - 120 - dx, dy );
		else winpic.setPos( 120, dy );
	}
}
/*}}*/

/*设置图片{{*/
setpic = function( v ){
	var pic = gdip.bitmap( v );
	if( pic == null ){
		win.msgboxErr('读取图片失败！\r\n请检查文件路径是否正确、格式是否为支持的格式！',"错误");
		return false;
	}
	winpic.picturebox.setBitmap();
	adapt( pic.width, pic.height );
	bithwnd = pic.copyHandle();
	winpic.picturebox.setBitmap(bithwnd);
	
	//*内存回收
	pic.dispose();
	::DeleteObject(bithwnd);
	//*/
	return true;
}
/*}}*/

subscribe("ShowPic",function(...){
	var para = {...};
	var value = para[1];
	if( !value ){
		if( bithwnd ) ::DeleteObject(bithwnd);//内存回收
		winpic.picturebox.show(false);
	}
	else if( value != history){
		history = value;
		if( setpic( value ) ) winpic.picturebox.show();
	}
} )

subscribe("Hide",function(...){
	if( bithwnd ) ::DeleteObject(bithwnd);//内存回收
	history = null;
	winpic.picturebox.setBitmap();
	winpic.picturebox.show(false);
} )

winpic.picturebox.orphanWindow();
winpic.picturebox.show(false);
win.loopMessage();
return winpic;
